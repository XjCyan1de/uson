plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
}

group = 'com.github.unidpers'
version = findProperty('github.tag') ?: 'undefined'
description = 'The simple fast json parser on pure java'

println("Building artifact: ")
println("${project.group}:${project.name}:${project.version}")

repositories {
    mavenCentral()
}

dependencies {
    compileOnly 'org.jetbrains:annotations:20.1.0'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
}

tasks {
    jar {
        archiveFileName = "Uson.jar"
    }

    java {
        withJavadocJar()
        withSourcesJar()

        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    signing {
        sign publishing.publications
    }
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            pom {
                name = project.name
                description = project.description

                url = 'https://github.com/unidpers/uson'

                organization {
                    name = 'com.github.unidpers'
                    url = 'https://github.com/unidpers'
                }

                issueManagement {
                    system = 'GitHub'
                    url = 'https://github.com/unidpers/uson/issues'
                }

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                scm {
                    url = 'https://github.com/unidpers/uson'
                    connection = 'scm:https://github.com/unidpers/uson.git'
                    developerConnection = 'scm:git://github.com/unidpers/uson.git'
                }

                developers {
                    developer {
                        id = 'unidpers'
                        name = 'Unidentified Person'
                        email = 'unidpers@gmail.com'
                    }
                }
            }

            groupId = project.group
            artifactId = project.name
            version = project.version

            from components.java
        }
    }

    repositories {
        maven {
            if (project.version.endsWith("-SNAPSHOT")) {
                url = "https://oss.sonatype.org/content/repositories/snapshots"
            } else {
                url = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            }

            credentials {
                username = findProperty('sonatype.user')
                password = findProperty('sonatype.password')
            }
        }
    }
}